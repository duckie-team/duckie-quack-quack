apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                pom.withXml {
                    asNode().children().last() + {
                        // Cannot create a Publication named 'DELEGATE_FIRST'.
                        // because this container does not support creating elements by name alone.
                        // noinspection UnnecessaryQualifiedReference
                        resolveStrategy = Closure.DELEGATE_FIRST

                        name PUBLISH_ARTIFACT_ID
                        description PUBLISH_DESCRIPTION
                        url PUBLISH_URL

                        licenses {
                            license {
                                name PUBLISH_LICENSE_NAME
                                url PUBLISH_LICENSE_URL
                            }
                        }

                        developers {
                            developer {
                                id PUBLISH_DEVELOPER_ID
                                name PUBLISH_DEVELOPER_NAME
                                email PUBLISH_DEVELOPER_EMAIL
                            }
                        }

                        scm {
                            connection PUBLISH_SCM_CONNECTION
                            developerConnection PUBLISH_SCM_DEVELOPER_CONNECTION
                            url PUBLISH_SCM_URL
                        }

                        dependencyManagement {
                            dependencies {
                                project.configurations.each { configuration ->
                                    configuration.dependencies.each { dependency ->
                                        dependency {
                                            groupId "${dependency.group}"
                                            artifactId "${dependency.name}"
                                            version "${dependency.version}"
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
